plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
    id 'com.hiya.jacoco-android'
    id 'androidx.navigation.safeargs'
    id 'com.github.ben-manes.versions'
    id 'com.getkeepsafe.dexcount'
    id 'com.gladed.androidgitversion'
    id 'com.betomorrow.appcenter'
    id 'ru.cian.huawei-publish'
}

androidGitVersion {
    baseCode 1
    codeFormat 'MNNPPP'
    prefix 'my-pet-'
}

android {
    def globalConfiguration = rootProject.extensions.getByName("ext")
    compileSdkVersion globalConfiguration["androidCompileSdkVersion"]
    buildToolsVersion globalConfiguration["androidBuildToolsVersion"]

    defaultConfig {
        applicationId "com.ngs.mypet"
        minSdkVersion globalConfiguration["androidMinSdkVersion"]
        targetSdkVersion globalConfiguration["androidTargetSdkVersion"]
        versionName androidGitVersion.name()
        versionCode androidGitVersion.code()
        multiDexEnabled = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        debug {
            storeFile rootProject.file("debug_keystore.jks")
            storePassword "12345678"
            keyAlias "debugAlias"
            keyPassword "12345678"
        }
        ci {
            storeFile rootProject.file("my_pet_keystore.jks")
            storePassword System.getenv("mypet_storepass")
            keyAlias System.getenv("mypet_keyalias")
            keyPassword System.getenv("mypet_keypass")
        }
        local {
            storeFile rootProject.file("my_pet_keystore.jks")
            storePassword System.getenv("mypet_storepass")
            keyAlias System.getenv("mypet_keyalias")
            keyPassword System.getenv("mypet_keypass")
        }
    }

    buildTypes {
        debug {
            testCoverageEnabled true
            applicationIdSuffix ".debug"
            signingConfig = signingConfigs.debug
        }
        release {
            minifyEnabled true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if (isCI()) {
                signingConfig = signingConfigs.ci
            } else {
                signingConfig = signingConfigs.local
            }
        }
    }

    flavorDimensions "releaseCycle"

    productFlavors {
        stage {
            dimension "releaseCycle"
            applicationIdSuffix ".alpha"
            versionNameSuffix "-alpha"
        }

        googlePlayStore {
            dimension "releaseCycle"
        }

        huaweiAppGallery {
            dimension "releaseCycle"
        }
    }

    variantFilter { variant ->
        def names = variant.flavors*.name
        def type = variant.buildType.name
        def isDebug = names.contains("stage")
        if (type.contains("debug") && !isDebug) {
            setIgnore(true)
        }
        if (type.contains("release") && (isDebug)) {
            setIgnore(true)
        }
    }

    dexOptions {
        preDexLibraries = false
        dexInProcess = false
        javaMaxHeapSize "4g"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
    }

    lintOptions {
        quiet true
        abortOnError false
        ignoreWarnings true
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

kapt {
    correctErrorTypes = true
}

configurations.all {
    resolutionStrategy {
        force "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    }
}

dependencies {
    implementation 'pl.droidsonroids.gif:android-gif-drawable:1.1.7'
    implementation 'com.google.android.gms:play-services-ads:19.5.0'
    implementation 'androidx.viewpager2:viewpager2:1.0.0'
    implementation "com.google.android.gms:play-services-location:17.1.0"
    implementation 'com.google.android.gms:play-services-maps:17.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'com.google.firebase:firebase-ads:19.5.0'
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'de.hdodenhof:circleimageview:3.1.0'
    def mobileUiDependencies = rootProject.ext.mobileUiDependencies
    def mobileUiTestDependencies = rootProject.ext.mobileUiTestDependencies

    implementation project(':presentation')
    implementation project(':domain')
    implementation project(':data')
    implementation project(':cache')
    implementation project(':remote')

    implementation mobileUiDependencies.javaxAnnotation

    implementation mobileUiDependencies.kotlin
    implementation mobileUiDependencies.javaxInject
    implementation mobileUiDependencies.rxKotlin
    implementation mobileUiDependencies.androidAnnotations
    implementation mobileUiDependencies.androidSupportV4
    implementation mobileUiDependencies.androidSupportV13
    implementation mobileUiDependencies.appCompatV7
    implementation mobileUiDependencies.supportRecyclerView
    implementation mobileUiDependencies.supportDesign
    implementation mobileUiDependencies.timber
    implementation mobileUiDependencies.rxAndroid
    implementation mobileUiDependencies.glide
    implementation mobileUiDependencies.dagger
    implementation mobileUiDependencies.daggerSupport
    implementation mobileUiDependencies.daggerAndroid
    implementation 'com.yarolegovich:discrete-scrollview:1.4.9'
    implementation "androidx.navigation:navigation-fragment-ktx:2.3.1"
    implementation "androidx.navigation:navigation-ui-ktx:2.3.1"
    implementation "com.jakewharton.rxbinding3:rxbinding:3.1.0"
    implementation presentationDependencies.archRuntime
    implementation presentationDependencies.archExtensions
    implementation "android.arch.persistence.room:rxjava2:1.1.1"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0'
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    testImplementation mobileUiTestDependencies.junit
    androidTestImplementation mobileUiTestDependencies.androidXTest
    androidTestImplementation mobileUiTestDependencies.androidXEspresso
    kapt presentationDependencies.archCompiler

    testImplementation mobileUiTestDependencies.kotlinJUnit

    kapt mobileUiDependencies.daggerCompiler
    kapt mobileUiDependencies.daggerProcessor
    compileOnly mobileUiDependencies.glassfishAnnotation

    // Instrumentation test dependencies
    androidTestImplementation mobileUiTestDependencies.junit
    androidTestImplementation mobileUiTestDependencies.mockito
    androidTestImplementation mobileUiTestDependencies.mockitoAndroid
    androidTestImplementation(mobileUiTestDependencies.espressoCore) {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation(mobileUiTestDependencies.androidRunner) {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation(mobileUiTestDependencies.androidRules) {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation(mobileUiTestDependencies.espressoIntents) {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation(mobileUiTestDependencies.espressoContrib) {
        exclude module: 'appcompat'
        exclude module: 'appcompat-v7'
        exclude module: 'support-v4'
        exclude module: 'support-v13'
        exclude module: 'support-annotations'
        exclude module: 'recyclerview-v7'
        exclude module: 'design'
    }
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.okhttp3:okhttp:4.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.0'

    implementation 'com.github.nqnhat1988.huawei_kits_demo:gms-services:1.1.1'
    kaptTest mobileUiDependencies.daggerCompiler
    kaptAndroidTest mobileUiDependencies.daggerCompiler
}

androidExtensions {
    experimental = true
}

appcenter {
    distributionGroups = ["Internal"]
    releaseNotes = rootProject.file("build/CHANGELOG.md")
    notifyTesters = true
    apps {
        stage {
            dimension = "releaseCycle"
            appName = "My-Pet-Stage-D"
            distributionGroups = ["Internal"]
        }

        googlePlayStore {
            dimension = "releaseCycle"
            appName = "My-Pet-Play-Store"
            distributionGroups = ["Internal","Manager","Enterprise"]
        }

        huaweiAppGallery {
            dimension = "releaseCycle"
            appName = "My-Pet-App-Gallery"
            distributionGroups = ["Internal","Manager","Enterprise"]
        }
    }
}

huaweiPublish {
    instances {
        huaweiAppGalleryRelease {
            credentialsPath = "$rootProject/hw-credentials.json"
            buildFormat = "aab"
            publish = false
        }
        huaweiAppGalleryDebug {
            credentialsPath = "$rootProject/hw-credentials.json"
            buildFormat = "aab"
            publish = false
        }
    }
}

task buildHuaweiCredentials {
    outputs.file rootProject.file("hw-credentials.json")
    doLast {
        def initFile = rootProject.file("hw-credentials.json")
        def clientId = System.getenv("mypet_client_id")
        def clientKey = System.getenv("mypet_client_key")
        initFile.text = """
                    {
                      "client_id": "$clientId",
                      "client_key": "$clientKey"
                    }
                    """
    }
}